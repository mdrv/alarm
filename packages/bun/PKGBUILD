# Contributor: Daniele Basso <d dot bass 05 at proton dot me>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer (aarch64): mdrv

pkgname=bun
pkgver=1.3.9
_webkitver=8af7958ff0e2a4787569edf64641a1ae7cfe074a
_zigver=c1423ff3fc7064635773a4a4616c5bf986eb00fe
pkgrel=2
pkgdesc="Incredibly fast JavaScript runtime, bundler, test runner, and package manager â€“ all in one"
arch=('aarch64')
url="https://github.com/oven-sh/bun"
license=('MIT')
depends=(
  gcc-libs
  glibc
  icu
  sqlite
)
makedepends=(
  cargo   # bun
  clang   # bun, webkit
  clang20 # zig
  cmake
  git
  lld    # bun, webkit
  lld20  # zig
  llvm   # bun
  llvm20 # zig
  ninja
  python          # webkit
  ruby            # webkit
  ruby-getoptlong # webkit
)
options=('!lto')
source=(
  "git+$url.git#tag=bun-v$pkgver"
  # Necessary for code generation
  "$pkgname-linux-aarch64-$pkgver.zip::https://github.com/oven-sh/bun/releases/download/bun-v$pkgver/bun-linux-aarch64.zip"
  "$pkgname-WebKit::git+https://github.com/oven-sh/WebKit.git#commit=$_webkitver"
  "$pkgname-zig::git+https://github.com/oven-sh/zig.git#commit=$_zigver"
  "$pkgname-add-cmake-option-zig-local.patch"
  "$pkgname-sqlite3-lib-path.diff"
)
b2sums=('d5f3c8ae6d3d25b34216efbfd0996d24673ccb478bcb2a920a174906a512079905fcfc2b9af3abf093b27cafc23df0db783addbae84d98641193004ffab9135b'
        'SKIP'  # Placeholder for aarch64 binary checksum
        '6b31bcabcf1cbef002d0eb824b3e4ad62127447849a748389ef304440126e2494762f89a10b8a2fc66dd0df9d146717b4e5ca7b2c9597c184a6e1bdbe35fd472'
        '8c867cf8d1a4895c366e9230cedd2328f7d6ee73be89cb58e794a8f226bb57c2ca7323650c4f275f8b502df4c1e2eec90c295b1079b6865d09d9ec8d8170483'
        'fb11b321280b79ef2807c8a3947dc8ec3e413bf53471fce7891923f8bb5bcb555c672638143d838d7af6229a147dcd2f8b9a76107a35f09146b7e2da88a2cd45'
        'f1ad976393570be7f09e0391d68a4f573735ddc13be577f8d3746f8adb71d66b74d705429a8bd5c3582107971a5ded1931303185dae6763251ad7ce4ce9e2cd5')

prepare() {
  cd $pkgname
  grep -q "WEBKIT_VERSION $_webkitver" cmake/tools/SetupWebKit.cmake \
    || (echo "WebKit version mismatch"; exit 1)
  grep -q "ZIG_COMMIT \"$_zigver\"" cmake/tools/SetupZig.cmake \
    || (echo "Zig version mismatch"; exit 1)
  patch -Np1 < ../$pkgname-add-cmake-option-zig-local.patch
  patch -Np1 < ../$pkgname-sqlite3-lib-path.diff
}

build() {
  local cmake_args_common=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -Wno-dev
    -GNinja
  )

  export PATH="/usr/lib/llvm/bin:$PATH"
  CC=clang CXX=clang++ \
    cmake -S bun-WebKit -B build-WebKit \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=JSCOnly \
    -DENABLE_STATIC_JSC=ON \
    -DUSE_BUN_JSC_ADDITIONS=ON \
    -DUSE_BUN_EVENT_LOOP=ON \
    -DENABLE_FTL_JIT=ON \
    -DALLOW_LINE_AND_COLUMN_NUMBER_IN_BUILTINS=ON \
    -DENABLE_REMOTE_INSPECTOR=ON
  cmake --build build-WebKit --target jsc

  PATH="/usr/lib/llvm20/bin:$PATH" \
    cmake -S bun-zig -B build-zig \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=None \
    -DZIG_PIE=ON \
    -DZIG_SHARED_LLVM=ON \
    -DZIG_USE_LLVM_CONFIG=ON \
    -DZIG_TARGET_TRIPLE=native-linux.6.6-gnu.2.40 \
    -DZIG_TARGET_MCPU=baseline
  cmake --build build-zig

  rm build-WebKit/JavaScriptCore/DerivedSources/inspector/InspectorProtocolObjects.h

  mkdir -vp bun/vendor/zig
  ln -vsf "$PWD/build-zig/stage3/bin/zig" bun/vendor/zig/zig
  ln -vsf "$PWD/build-zig/stage3/lib/zig" bun/vendor/zig/lib
  ln -vsf "$PWD/bun-WebKit" bun/vendor/WebKit

  # Link system ICU libs - bun expects .a but works with .so
  ln -vs /usr/lib/libicudata.so build-WebKit/lib/libicudata.a
  ln -vs /usr/lib/libicui18n.so build-WebKit/lib/libicui18n.a
  ln -vs /usr/lib/libicuuc.so build-WebKit/lib/libicuuc.a

  export PATH="$PWD/bun-linux-aarch64:$PATH"
  bun run --cwd=$pkgname scripts/glob-sources.mjs

  local llvmver=$(clang --version | head -1 | awk '{print $3}')
  cmake -S bun -B build-bun \
    "${cmake_args_common[@]}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_CANARY=OFF \
    -DLLVM_VERSION="$llvmver" \
    -DENABLE_LTO=ON \
    -DENABLE_ASAN=OFF \
    -DUSE_STATIC_SQLITE=OFF \
    -DUSE_STATIC_LIBATOMIC=OFF \
    -DWEBKIT_LOCAL=ON \
    -DWEBKIT_PATH="$PWD/build-WebKit" \
    -DZIG_LOCAL=ON
  cmake --build build-bun
}

check() {
  # Smoke test
  ./build-bun/bun --version
  echo 'console.log("ok")' | ./build-bun/bun run -
}

package() {
  install -vDm755 build-bun/bun "$pkgdir/usr/bin/bun"
  ln -vs /usr/bin/bun "$pkgdir/usr/bin/bunx"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" bun/LICENSE.md

  install -vDm644 bun/completions/bun.bash "$pkgdir/usr/share/bash-completion/completions/bun"
  install -vDm644 bun/completions/bun.fish "$pkgdir/usr/share/fish/vendor_completions.d/bun.fish"
  install -vDm644 bun/completions/bun.zsh "$pkgdir/usr/share/zsh/site-functions/_bun"
}
