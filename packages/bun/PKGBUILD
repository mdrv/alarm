# Maintainer (aarch64): mdrv
# Based on bun-bin from AUR by Noel Jacob <noeljacob91 at gmail dot com>
pkgname=bun
pkgver=1.3.9
pkgrel=1
pkgdesc="All-in-one JavaScript runtime built for speed, with bundler, transpiler, test runner, and package manager. Includes bunx and shell completions"
arch=('aarch64')
url="https://github.com/oven-sh/bun"
license=('MIT')
provides=('bun')
conflicts=('bun')
source=("bun-aarch64-${pkgver}.zip::https://github.com/oven-sh/bun/releases/download/bun-v${pkgver}/bun-linux-aarch64.zip"
        "LICENSE")
sha256sums=('SKIP'
            'SKIP')

build() {
  cd "bun-linux-aarch64"
  install -dm755 "completions"
  SHELL=zsh "./bun" completions >"completions/bun.zsh"
  SHELL=bash "./bun" completions >"completions/bun.bash"
  SHELL=fish "./bun" completions >"completions/bun.fish"
}

package() {
  cd "${srcdir}/bun-linux-aarch64"
  install -Dm755 "./bun" "${pkgdir}/usr/bin/bun"
  ln -s bun "${pkgdir}/usr/bin/bunx"
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "completions/bun.zsh" "${pkgdir}/usr/share/zsh/site-functions/_bun"
  install -Dm644 "completions/bun.bash" "${pkgdir}/usr/share/bash-completion/completions/bun"
  install -Dm644 "completions/bun.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/bun.fish"
}
